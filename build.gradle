def javaVersion    = '1.7';
def projectVersion = '0.9.0';

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java'
    apply plugin: 'maven'

    project.group = 'org.develnext.jphp'
    project.version = projectVersion
    project.ext.isSnapshot = !Boolean.getBoolean("release");

    if (project.ext.isSnapshot) {
        version += '-SNAPSHOT'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.12;
}

configure(subprojects) {
    apply plugin: 'maven'
    apply plugin: 'java'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    sourceSets {
        main.java.srcDirs = ['src/main/java']
        main.resources.srcDirs = ['src/main/resources']

        test.java.srcDirs = ["src/main/tests"]
        test.resources.srcDirs = ["src/main/tests"]
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}
